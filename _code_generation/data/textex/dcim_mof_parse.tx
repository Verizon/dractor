// Copyright (C) 2017 Verizon. All Rights Reserved.
//
//     File:    dcim_mof_parse.tx
//     Author:  John Hickey
//     Date:    2017-02-17
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//       http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.


// DCIM MOF parsing
root:
    MOFClass
;

MOFClass:
    ('[' qualifiers+=QualifierTypes[','] ']')?
    'class' name=ID (':' parent_class=ID)? '{'
        members*=Function
    '};'
;

Function:
    ('[' qualifiers+=QualifierTypes[','] ']')?
    return_type=ID ('REF')? name=/\w+(\[\])?/
    ('(' arguments+=FunctionArg[','] ')')?
    ('=' default=/\w+/)?
    ';'
;

FunctionArg:
    ('[' qualifiers+=QualifierTypes[','] ']')?
    ctype=ID ('REF')? name=/\w+(\[\])?/
;

QualifierTypes:
    (StringArg | IntArg | List | NegativeKeyword | Keyword)
;

StringArg:
    name=ID '(' values+=STRING ')'
;

// For MaxLen, etc
IntArg:
    name=ID '(' value=INT ')'
;

// Lists used by mappings
List:
    name=ID '{' values+=STRING[','] '}'
;

NegativeKeyword:
    name=ID '(' 'false' ')'
;

Keyword:
    name=ID
;

// Base
Comment:
  /\/\/.*$/
;
